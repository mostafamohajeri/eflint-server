Fact subject 
Fact data    
Fact subject-of Identified by subject * data

Fact controller
Fact processor
Fact processes Identified by 
  processor * data * controller * purpose
Fact purpose
Fact consent Identified by 
  subject * controller * purpose
Fact accurate-for-purpose Identified by
  data * purpose

Act give-consent
  Actor subject
  Recipient controller
  Related to purpose
  Creates consent()
  Holds when !consent()

Act collect-personal-data
  Actor controller
  Recipient subject
  Related to data, processor, purpose 
    When subject-of()
  Creates processes()
  Holds when consent() && accurate-for-purpose()

Act demand-rectification
 Actor subject
 Recipient controller
 Related to purpose
 Creates rectification-duty()
 Holds when (Exists data, processor: 
  subject-of() && processes() && !accurate-for-purpose())

Duty rectification-duty
  Holder controller
  Claimant subject
  Related to purpose
  Violated when undue-rectification-delay()
Fact undue-rectification-delay Identified by
  controller * purpose * subject 

Act rectified-personal-data
 Actor controller
 Recipient subject
 Related to purpose 
 Terminates rectification-duty()
           ,undue-rectification-delay()
 Holds when processors-accurate()

Fact processors-accurate
 Identified by controller * subject * purpose
 Holds when (Forall processor, data : 
               accurate-for-purpose() 
               When processes() && subject-of())

Placeholder new-data For data
Event data-change 
 Related to data, new-data, purpose
  When data != new-data
    && subject-of() && subject-of(data = new-data)
 Terminates accurate-for-purpose(data,purpose)
 Creates    accurate-for-purpose(new-data,purpose)
 Holds when accurate-for-purpose(data,purpose)

Event rectification-delay
 Related to controller, purpose, subject 
 Creates undue-rectification-delay() 
          When rectification-duty()

.
Fact subject    Identified by Alice, Bob
Fact controller Identified by Bank 
Fact processor  Identified by Processor
Fact data       Identified by A1, A2, A3, B1
Fact purpose    Identified by ClientProfile 

.

+subject-of(Alice, A1).
+subject-of(Alice, A2).
+subject-of(Alice, A3).
+subject-of(Bob, B1).
+accurate-for-purpose(A1, purpose).
+accurate-for-purpose(B1, purpose).

give-consent(subject = Alice).                                   // Alice gives consent to the bank
collect-personal-data(subject = Alice, data = A1).               // the bank collects her data
?!Enabled(collect-personal-data(subject = Bob, data = A1)).      // Bob is not the subject of A1
?!Enabled(collect-personal-data(subject = Bob, data = B1)).      // Bob has not given consent
data-change(data = A1, new-data = A2).                           // Alice's information changes
demand-rectification(subject = Alice, data = A1).                // Alice demands rectification
?!Violated(rectification-duty(subject = Alice)).                 // the resulting duty is not violated
rectification-delay(subject = Alice).                            // too much time has passed
?Violated(rectification-duty(subject = Alice)).                  // the duty is now violated
-processes(data=A1).
+processes(data=A2).
rectified-personal-data(subject = Alice).                        // processor rectifies the data
?!rectification-duty(subject = Alice).                           // the duty is no longer present 
